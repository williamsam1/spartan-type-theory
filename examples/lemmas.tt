Load "examples/equality.tt".
Load "examples/numerals.tt".

(** n + 0 == n *)
Definition z_id_r := (nat_ind (λ (n : Nat) ⇒ Eq (n + Z) n)
    (Refl Z)
    (λ (n : Nat) (p : Eq (n + Z) n) ⇒ ap Nat Nat S (n + Z) n p))
    : Π (n : Nat), Eq (n + Z) n.
(**
z_id_r : (n : Nat) n + 0 == n
z_id_r 0 = Refl 0
z_id_r (S n) = ap Nat Nat S (n + 0) n (z_id_r n)
*)

(** m + n == n + m *)
Definition plus_comm :=
    (nat_ind (λ (m : Nat) ⇒ Π (n : Nat), Eq (m + n) (n + m))
        (λ (n : Nat) ⇒ sym Nat (n + Z) n (z_id_r n))
        (λ (m : Nat) (p : Π (n : Nat), Eq (m + n) (n + m)) ⇒
            nat_ind (λ (n : Nat) ⇒ Eq (S(m) + n) (n + S(m)))
                (ap Nat Nat S (m + Z) m (z_id_r m))
                (λ (n : Nat) (q : Eq (S(m) + n) (n + S(m))) ⇒ 
                    ap Nat Nat S (m + (S n)) (n + (S m))
                    (trans2 Nat (m + (S n)) ((S n) + m) ((S m) + n) (n + (S m))
                        (p (S n))
                        (ap Nat Nat S (n + m) (m + n) (sym Nat (m + n) (n + m) (p n)))
                        q
                    ))
        )
    )
    : Π (m n : Nat), Eq (m + n) (n + m).

(** f 0 == a → f (S n) = S (f n) → f n == n + a *)
Definition lin_λc := (λ (f : Nat → Nat) (a : Nat) (f0 : Eq (f Z) a)
    (fS : Π (n : Nat), Eq (f (S n)) (S (f n))) ⇒
    nat_ind (λ (n : Nat) ⇒ Eq (f n) (n + a))
    f0
    (λ (n : Nat) (fn : Eq (f n) (n + a)) ⇒ tp Nat (λ (x : Nat) ⇒ Eq (f (S n)) (S x)) (f n) (n + a) fn (fS n)))
    : Π (f : Nat → Nat) (a : Nat),
        Eq (f Z) a
        → (Π (n : Nat), Eq (f (S n)) (S (f n)))
        → Π (n : Nat), Eq (f n) (n + a).

(** f x y1 == f x y2 → g x == f x b → g x == f x y *)
Definition irrelev := (λ (A B C : Type) (f : A → B → C) (g : A → C) (b : B)
    (p : Π (a : A) (b1 b2 : B), Eq (f a b1) (f a b2))
    (q : Π (a : A), Eq (g a) (f a b))
    (x : A) (y : B) ⇒ trans C (g x) (f x b) (f x y) (q x) (p x b y))
    : Π (A B C : Type) (f : A → B → C) (g : A → C) (b : B),
        (Π (a : A) (b1 b2 : B), Eq (f a b1) (f a b2))
        → (Π (a : A), Eq (g a) (f a b))
        → Π (x : A) (y : B), Eq (g x) (f x y).